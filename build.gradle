plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'java'
}

// Versioning
def versionNumber = '0.1'
def versionType = 'DEV' // RELEASE | BETA | ALPHA | DEV
def versionMarker = versionType.charAt(0)

group = 'it.italiandudes'
version = versionNumber+versionMarker

repositories {
    mavenCentral()
    flatDir {
        dirs 'lib'
    }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:24.1.0'
    implementation 'commons-io:commons-io:2.17.0'
    implementation 'it.italiandudes:idl:1.0.5'
    implementation 'org.json:json:20231013'
}

shadowJar {
    manifest {
        attributes "Main-Class" : "it.italiandudes.mctranslationmapper.MCTranslationMapper"
        attributes "Title" : "MCTranslationMapper"
        attributes "Vendor" : "ItalianDudes"
        attributes "Version" : archiveVersion.get()
        attributes "Version-Number" : archiveVersion.get()
        attributes "Version-Type" : versionType
        attributes "Version-Marker" : versionMarker
        attributes "Timestamp" : new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    }
    archiveFileName = getRootDir().getName()+"-"+archiveVersion.get()+".jar"
}

tasks.register('buildJar') {
    dependsOn shadowJar
    group = "Build"
    description = "Build and assemble the MCTranslationMapper"
}

tasks.register('runClient', Exec) {
    dependsOn shadowJar
    group = "Execution"
    description = "Build, assemble and run the MCTranslationMapper"
    standardInput = System.in
    commandLine "java", "-jar", jar.archiveFile.get()
}